import javax.net.ssl.HttpsURLConnection
import java.nio.charset.StandardCharsets

subprojects {
    apply plugin: 'java'
    
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()
        
    jar {
        archiveClassifier.set("slim")
        from(rootProject.file("LICENSE"))
        from(rootProject.file("COPYING"))
        from(rootProject.file("COPYING.LESSER"))
        from(rootProject.file("README.md"))
        from(rootProject.file("CHANGELOG.md"))
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestampe'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    sourcesJar {
        from(rootProject.file("LICENSE"))
        from(rootProject.file("COPYING"))
        from(rootProject.file("COPYING.LESSER"))
        from(rootProject.file("README.md"))
        from(rootProject.file("CHANGELOG.md"))
    }

    repositories {
        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
		
		maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org/'
        }

        maven {
            name = 'Illusive Soulworks'
            url = 'https://maven.theillusivec4.top/'
        }
        mavenLocal()
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    tasks.withType(Jar).all {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

def publishDiscord() {
    try {
        def cfLinks = new StringJoiner('\\n')

        if (project(':fabric').hasProperty('curse_link')) {
            cfLinks.add("[Fabric](${project(':fabric').findProperty('curse_link')})")
        }

        if (project(':forge').hasProperty('curse_link')) {
            cfLinks.add("[Forge](${project(':forge').findProperty('curse_link')})")
        }

        if (project(':quilt').hasProperty('curse_link')) {
            cfLinks.add("[Quilt](${project(':quilt').findProperty('curse_link')})")
        }
        def modrinthLinks = new StringJoiner('\\n')

        if (project(':fabric').hasProperty('modrinth_link')) {
            modrinthLinks.add("[Fabric](${project(':fabric').findProperty('modrinth_link')})")
        }

        if (project(':forge').hasProperty('modrinth_link')) {
            modrinthLinks.add("[Forge](${project(':forge').findProperty('modrinth_link')})")
        }

        if (project(':quilt').hasProperty('modrinth_link')) {
            modrinthLinks.add("[Quilt](${project(':quilt').findProperty('modrinth_link')})")
        }
        println(cfLinks)
        println(modrinthLinks)
        def changelog = file("CHANGELOG_LATEST.md").getText()
        changelog = changelog.substring(changelog.indexOf("##"))
        changelog = changelog.replaceAll("\n","\\\\n")

        if (changelog.length() >= 1024) {
            changelog = changelog.substring(0, changelog.length() - 100)
            changelog = changelog + "...[(See more)](${changelog_link})"
        }
        println(changelog)
        final String message = """
            {
              "embeds": [
                {
                  "title": "${mod_name} ${version}",
                  "color": 65392,
                  "fields": [
                    {
                      "name": "Minecraft Versions",
                      "value": "${release_versions.replaceAll(",", ", ")}"
                    },
                    {
                      "name": "CurseForge",
                      "value": "${cfLinks}",
                      "inline": true
                    },
                    {
                      "name": "Modrinth",
                      "value": "${modrinthLinks}",
                      "inline": true
                    },
                    {
                      "name": "Changelog",
                      "value": "${changelog}"
                    }
                  ],
                  "thumbnail": {
                    "url": "${discord_thumbnail}"
                  }
                }
              ]
            }
            """
        println(message)
        final URL url = new URL("${discordWebhook}")
        final HttpsURLConnection connection = (HttpsURLConnection) url.openConnection()
        connection.addRequestProperty("Content-Type", "application/json; charset=UTF-8")
        connection.addRequestProperty("User-Agent", "${mod_name} Gradle Upload")
        connection.setDoOutput(true)
        connection.setRequestMethod("POST")
        connection.connect()

        try (OutputStream out = connection.getOutputStream()) {
            out.write(message.getBytes(StandardCharsets.UTF_8))
        }
        connection.getInputStream().close()
        connection.disconnect()
    } catch (IOException e) {
        e.printStackTrace()
    }
}

tasks.getByPath(':quilt:publishCurseForge').configure {
    mustRunAfter(':quilt:assemble')
}
tasks.getByPath(':fabric:publishCurseForge').configure {
    mustRunAfter(':fabric:assemble')
    mustRunAfter(':quilt:publishCurseForge')
}
tasks.getByPath(':forge:publishCurseForge').configure {
    mustRunAfter(':forge:assemble')
    mustRunAfter(':fabric:publishCurseForge')
}
tasks.getByPath(':fabric:modrinth').mustRunAfter(':quilt:modrinth')
tasks.getByPath(':forge:modrinth').mustRunAfter(':fabric:modrinth')

tasks.register('publishFabricForge') {
    dependsOn ':fabric:modrinth', ':forge:modrinth'
    dependsOn ':fabric:publishCurseForge', ':forge:publishCurseForge'

    doLast {
        publishDiscord()
    }
}

tasks.register('publishFabricForgeQuilt') {
    dependsOn ':fabric:modrinth', ':forge:modrinth', ':quilt:modrinth'
    dependsOn ':fabric:publishCurseForge', ':forge:publishCurseForge', ':quilt:publishCurseForge'

    doLast {
        publishDiscord()
    }
}
