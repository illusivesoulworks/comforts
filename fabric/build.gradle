plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "${project.archives_base_name}-fabric"
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
    maven {
        url = "https://maven.theillusivec4.top"
    }
    maven {
        name = "Ladysnake Libs"
        url = "https://dl.bintray.com/ladysnake/libs"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // Required
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation fabricApi.module("fabric-networking-blockentity-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-item-groups-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-renderer-registries-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-textures-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-registry-sync-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cca_version}"
    modImplementation "top.theillusivec4.somnus:somnus-fabric:${project.somnus_version}"
    modImplementation "me.shedaniel.cloth:config-2:${project.cloth_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
    modImplementation "me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Embedded
//    include fabricApi.module("fabric-networking-v0", project.fabric_version)
//    include fabricApi.module("fabric-networking-blockentity-v0", project.fabric_version)
//    include fabricApi.module("fabric-item-groups-v0", project.fabric_version)
//    include fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
//    include fabricApi.module("fabric-renderer-registries-v1", project.fabric_version)
//    include fabricApi.module("fabric-textures-v0", project.fabric_version)
//    include fabricApi.module("fabric-registry-sync-v0", project.fabric_version)
//    include fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cca_version}"
    include "top.theillusivec4.somnus:somnus-fabric:${project.somnus_version}"
    include "me.shedaniel.cloth:config-2:${project.cloth_version}"
    include "me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig_version}"

    // Optional
    modCompileOnly "io.github.prospector:modmenu:${project.modmenu_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntime "io.github.prospector:modmenu:${project.modmenu_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

sourceSets {
    main {
        resources {
            srcDirs += '../docs'
        }
    }
}

curseforge {
    project {
        apiKey = findProperty('curseKey') ?: 0
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        addGameVersion "Java 8"
        addGameVersion "Fabric"
        addGameVersion project.minecraft_version
        addArtifact(sourcesJar)
        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false
    }
}
