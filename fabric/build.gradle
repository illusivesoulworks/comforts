plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.archives_base_name}-fabric"
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
    maven {
        name = "Ladysnake Libs"
        url = "https://ladysnake.jfrog.io/artifactory/mods"
    }
    maven {
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // Required
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-item-groups-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-renderer-registries-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-textures-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-registry-sync-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-object-builder-api-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-entity-events-v1", project.fabric_version)
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cca_version}"

    // Embedded
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cca_version}"

    // Optional
    modCompileOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
    modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntimeOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"
    modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
    modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    from("CHANGELOG.md")
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

sourceSets {
    main {
        resources {
            srcDirs += '../docs'
        }
    }
}

curseforge {
    project {
        apiKey = findProperty('curseKey') ?: 0
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        addGameVersion "Java 17"
        addGameVersion "Fabric"
        "${curse_versions}".split(',').each {
            addGameVersion "${it}"
        }
        addArtifact(sourcesJar)
        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))

        relations {
            requiredDependency 'fabric-api'
            optionalDependency 'cloth-config'
            optionalDependency 'modmenu'
            embeddedLibrary 'cardinal-components'
        }

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false
    }
}
